ENTRY(EntryPoint)
MEMORY
{
    IRAM : ORIGIN = 0x40000000, LENGTH = 0x00010000
    SDRAM : ORIGIN = 0xA0000000, LENGTH = 0x01000000
    LR_FLASH : ORIGIN = 0x00000000, LENGTH = 0x00080000    
}
SECTIONS
{
    ER_FLASH 0x00004400 :
    {
        * (i.EntryPoint)
        * (SectionForBootstrapOperations)
        * (SectionForFlashOperations)
        * (.text*)
        * (i.*)
        * (.rodata*)
        * (rodata)
        * (.constdata*)
        * (.conststring*)
        * (.glue*)
        * (tinyclr_metadata)
        PROVIDE(_sbrk = .);
        PROVIDE(_write = .);
        PROVIDE(_close = .);
        PROVIDE(_fstat = .);
        PROVIDE(_lseek = .);
        PROVIDE(_read = .);
        PROVIDE(_exit = .);
        PROVIDE(_getpid = .);
        PROVIDE(_kill = .);
        PROVIDE(abort = .);
        PROVIDE(__errno = .);
        PROVIDE(_read = .);
        PROVIDE(isatty = .);
        PROVIDE(_isatty = .);
    }>LR_FLASH

    ER_VECTOR 0x40000000 :
    {
        *(VectorsTrampolines)
    }>IRAM

    ER_RAM_RO 0xA0030000 : ALIGN(0x8)
    {
        * (SectionForCodeReadOnlyRAM)
    }>SDRAM

    ER_RAM_RW 0xA0040000 : ALIGN(0x8)
    {
        * (rwdata)
        * (.data*)
    }>SDRAM AT>LR_FLASH

    .bss (NOLOAD) : ALIGN(0x8)
    {
        * (.bss.*)
        * (.bss2.*)
        * (.zidata*)
        PROVIDE(__exidx_start = .);
        PROVIDE(__exidx_end = .);
        * (COMMON)
    }>SDRAM

    /DISCARD/  :
    {
        * (.ARM.exidx*)
        * (.ARM.extab*)
    }

    ER_HEAP_BEGIN 0xA0200000 :
    {
        * (SectionForHeapBegin)
    }>SDRAM

    ER_HEAP_END 0xA0F00000 - 0x8 :
    {
        * (SectionForHeapEnd)
    }>SDRAM
    
    ER_RLP_BEGIN 0xA0F00000 : ALIGN(0x8)
    {
        * (SectionForRlpBegin)
    }>SDRAM
    
    ER_RLP_END 0xA1000000 - 0x08: 
    {
        * (SectionForRlpEnd)
    }>SDRAM

    ER_STACK_BOTTOM 0x40001000 : ALIGN(0x8)
    {
        * (SectionForStackBottom)
    }>IRAM

    ER_STACK_TOP 0x40010000 - 0x8: ALIGN(0x8)
    {
        * (SectionForStackTop +LAST)
    }>IRAM
}
Load$$ER_FLASH$$Base = LOADADDR(ER_FLASH);
Image$$ER_FLASH$$Length = SIZEOF(ER_FLASH);
Image$$ER_RAM_RO$$Base = ADDR(ER_RAM_RO);
Image$$ER_RAM_RO$$Length = SIZEOF(ER_RAM_RO);
Load$$ER_RAM_RO$$Base = LOADADDR(ER_RAM_RO);
Image$$ER_RAM_RW$$Base = ADDR(ER_RAM_RW);
Image$$ER_RAM_RW$$Length = SIZEOF(ER_RAM_RW);
Load$$ER_RAM_RW$$Base = LOADADDR(ER_RAM_RW);
Image$$ER_RAM_RW$$ZI$$Base = ADDR(.bss);
Image$$ER_RAM_RW$$ZI$$Length = SIZEOF(.bss);
Image$$ER_VECTOR$$Base = ADDR(ER_VECTOR);
Image$$ER_VECTOR$$Length = 0x00000040;
Load$$ER_RLP$$Base = LOADADDR(ER_RLP_BEGIN);
Image$$ER_RLP$$Length = LOADADDR(ER_RLP_END) - LOADADDR(ER_RLP_BEGIN);
__use_no_semihosting_swi = 0;
